# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=gnustep-base
pkgver=1.28.0
pkgrel=2
pkgdesc="The GNUstep base package"
arch=('x86_64')
url="http://www.gnustep.org/"
license=("GPL" "LGPL")
depends=(libxslt avahi gmp gcc-libs openssl libffi gnutls icu)
makedepends=(gcc-objc gnustep-make)
groups=('gnustep-core')
options=('!emptydirs' '!makeflags')
source=(https://github.com/gnustep/libs-base/releases/download/base-${pkgver//./_}/gnustep-base-${pkgver}.tar.gz
        https://github.com/gnustep/libs-base/commit/bd5f2909e6edc8012a0a6e44ea1402dfbe1353a4.patch
        icu68.patch)
sha256sums=('c7d7c6e64ac5f5d0a4d5c4369170fc24ed503209e91935eb0e2979d1601039ed'
            'd78be9d4ed3b00b1622c94289335716133e53d1f68bf8cd755fa3fa3deeed1b5'
            '0b38c3df1a150bc64fb4f37cd92cfc63fa7c0741a77eca940fb992942858d7d0')
validpgpkeys=('83AAE47CE829A4146EF83420CA868D4C99149679')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i ../icu68.patch
  patch -RNp1 -i ../bd5f2909e6edc8012a0a6e44ea1402dfbe1353a4.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  source /usr/share/GNUstep/Makefiles/GNUstep.sh
  ./configure --prefix=/usr --sysconfdir=/etc/GNUstep \
	--with-ffi-include=/usr/lib/libffi-`pacman -Q libffi | cut -f2 -d\ |cut -f1 -d-`/include/
  # fix file ownership
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' NSTimeZones/Makefile.postamble
  sed -i 's|.*gnutls_transport_set_lowat.*||' Source/GSSocketStream.m
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  chown -R root.root "$pkgdir/"
}
